tokens: 

LEFT_BRACE      :   \{
RIGHT_BRACE     :   \}
LEFT_BRACKET    :   \[
RIGHT_BRACKET   :   \]
COLON           :   :
COMMA           :   ,
BOOLEAN         :   (true|false)
NULL            :   null
STRING          :   "[^"\\]*(?:\\.[^"\\]*)*"
NUMBER          :   -?(?:0|[1-9]\d*)(?:\.\d+)?(?:[eE][+-]?\d+)?
WHITESPACE      :   \s+

grammar: 

<JSON>      =   BOOLEAN | NULL | STRING | NUMBER | <LIST> | <OBJ>
<LIST>      =   LEFT_BRACKET <JSON> <ITEMS> RIGHT_BRACKET | LEFT_BRACKET RIGHT_BRACKET
<ITEMS>     =   EPSILON | COMMA <JSON> <ITEMS>
<OBJ>       =   LEFT_BRACE STRING COLON <JSON> <ENTRIES> RIGHT_BRACE | LEFT_BRACE RIGHT_BRACE
<ENTRIES>   =   EPSILON | COMMA STRING COLON <JSON> <ENTRIES>

# This is a comment example
