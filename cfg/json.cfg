tokens: 

LEFT_BRACE      :   \{
RIGHT_BRACE     :   \}
LEFT_BRACKET    :   \[
RIGHT_BRACKET   :   \]
COLON           :   :
COMMA           :   ,
BOOLEAN         :   (true|false)
NULL            :   null
STRING          :   "[^"\\]*(?:\\.[^"\\]*)*"
NUMBER          :   -?(?:0|[1-9]\d*)(?:\.\d+)?(?:[eE][+-]?\d+)?
WHITESPACE      :   \s+

grammar: 

<JSON>          =   BOOLEAN | NULL | STRING | NUMBER | <LIST> | <OBJ>
<LIST>          =   LEFT_BRACKET <ITEMS> RIGHT_BRACKET
<ITEMS>         =   EPSILON | <JSON> <ITEMS_TAIL>   
<ITEMS_TAIL>    =   EPSILON | COMMA <JSON> <ITEMS_TAIL>
<OBJ>           =   LEFT_BRACE <ENTRIES> RIGHT_BRACE 
<ENTRIES>       =   EPSILON | STRING COLON <JSON> <ENTRIES_TAIL>
<ENTRIES_TAIL>  =   EPSILON | COMMA STRING COLON <JSON> <ENTRIES_TAIL>
